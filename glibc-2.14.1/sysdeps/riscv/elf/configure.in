GLIBC_PROVIDES dnl See aclocal.m4 in the top level source directory.
# Local configure fragment for sysdeps/mips/elf.

if test "$usetls" != no; then
# Check for support of thread-local storage handling in assembler and
# linker.
AC_CACHE_CHECK(for MIPS TLS support, libc_cv_mips_tls, [dnl
cat > conftest.s <<\EOF
	.section ".tdata", "awT", %progbits
	.globl foo
foo:	.long	1
	.section ".tbss", "awT", %nobits
	.globl bar
bar:	.skip	4
	.text

	lw	t7, %call16(__tls_get_addr)(gp)
	add	a0, gp, %tlsgd(x) 
	jalr	t7
EOF
dnl
if AC_TRY_COMMAND(${CC-cc} -c $CFLAGS conftest.s 1>&AS_MESSAGE_LOG_FD); then
  libc_cv_mips_tls=yes
else
  libc_cv_mips_tls=no
fi
rm -f conftest*])
if test $libc_cv_mips_tls = yes; then
  AC_DEFINE(HAVE_TLS_SUPPORT)
fi
fi

dnl No MIPS GCC supports accessing static and hidden symbols in an
dnl position independent way.
dnl AC_DEFINE(PI_STATIC_AND_HIDDEN)
